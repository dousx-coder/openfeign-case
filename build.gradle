plugins {
    //  https://plugins.gradle.org/
    id 'org.springframework.boot' version '2.7.1'
    id "io.spring.dependency-management" version "1.0.12.RELEASE"
    id 'java'
}
bootJar {
    // https://stackoverflow.com/questions/56861256/gradle-build-failed-main-class-name-has-not-been-configured-and-it-could-not-be
    enabled = false
}
jar {
    enabled = true
}
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
repositories {
    mavenLocal()
    maven { url 'https://maven.aliyun.com/repository/public' }
    mavenCentral()
}

ext {
    //自定义版本变量
    set('springBootVersion', "2.7.1")
    set('commonToolsVersion', "1.1.2022080821")
    set('logUtilVersion', "1.1.2022102116")
    set('openfeignVersion', "11.8")
    set('ribbonVersion', "2.7.18")
}

// 针对所有project的配置，包含根项目
allprojects {
    //项目是java项目
    apply plugin: 'java'
    //项目是idea项目
    apply plugin: 'idea'
    // 根项目中指定version
    group 'cn.cruder.dousx'
    // 根项目中指定version
    version '1.1-SNAPSHOT'
    // 指定java版本
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    jar.enabled = true
    /* java编译的时候缺省状态下会因为中文字符而失败 */
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
}
// 针对所有子project的配置
subprojects {
    // 所有项目都加了springboot相关插件,如果子项目是工具类,则配置 bootJar.enabled = false
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public' }
        mavenCentral()
    }
    // 版本管理
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
        // 指定版本
        dependencies {
            // 自定义工具类
            dependency "cn.cruder:common-tools:${commonToolsVersion}"
            // 自定义log starter
            dependency "cn.cruder:log-util-spring-boot-starter:${logUtilVersion}"
            dependency "io.github.openfeign:feign-core:${openfeignVersion}"
            dependency "io.github.openfeign:feign-jackson:${openfeignVersion}"
            dependency "io.github.openfeign:feign-ribbon:${openfeignVersion}"
        }
    }

    // 添加通用依赖
    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.projectlombok:lombok'
        implementation "cn.cruder:common-tools"
    }
}